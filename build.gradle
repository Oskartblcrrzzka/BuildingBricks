plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.1"
    id "com.matthewprenger.cursegradle" version "1.0.1"
}

repositories {
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }
}

version = "1.0.0"
group = "com.hea3ven.buildingbricks"
archivesBaseName = "BuildingBricks"

minecraft {
    version = "1.8-11.14.3.1513"
    runDir = "eclipse"

    mappings = "snapshot_20141130"

    replace "PROJECTVERSION", project.version
    replace "MCVERSION", project.minecraft.version
    replace "FORGEVERSION", project.minecraft.forgeVersion
}

curseforge {
    apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

    project {
        id = '236150'
        releaseType = 'release'
        changelog = ''
        addGameVersion '1.8'
    }
}

configurations {
    runmods
}

dependencies {
    runmods "codechicken:CodeChickenCore:1.8-1.0.5.34:dev"
    runmods "codechicken:NotEnoughItems:1.8-1.0.5.82:dev"

    testCompile "junit:junit:4.12"
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "forgeversion", project.minecraft.forgeVersion

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'forgeversion':project.minecraft.forgeVersion
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task copyRunMods(type: Copy) {
    from configurations.runmods.files
    into minecraft.runDir + "/mods/"
}
setupDecompWorkspace.dependsOn copyRunMods

task updateChangelog << {
    def changelogData = new File("CHANGELOG").getText()
    if (!changelogData.startsWith(project.version)) {
        new File("CHANGELOG").write(project.version + ":\n" + changelogData);
    }
}

task setCurseForgeProjectChangelog (dependsOn: 'updateChangelog') << {
    curseforge.curseProjects.each { proj ->
        proj.changelog = readCurrentChangelog()
    }
}
allprojects {
    afterEvaluate { project ->
        curseforge.curseProjects.each { proj ->
            proj.uploadTask.dependsOn setCurseForgeProjectChangelog
        }
    }
}

task updateReadme (dependsOn: 'curseforge') << {
    def mediafireUrl = System.console().readLine('> enter mediafire url: ')
    def curseFileId = System.console().readLine('> enter curse file id: ')

    new File("README.md.tmp").withWriter { w ->
        new File("README.md").eachLine { line ->
            if (line.startsWith("MediaFire: [Building Bricks")) {
                line = "MediaFire: [Building Bricks " + project.version + "]("+ mediafireUrl + ")"
            } else if (line.startsWith("Curse: [Building Bricks")) {
                line = "Curse: [Building Bricks " + project.version + "](http://minecraft.curseforge.com/mc-mods/236150-building-bricks/files/"+ curseFileId + ")"
            }
            w << line + "\n"
        }
    }

    new File("README.md").delete()
    new File("README.md.tmp").renameTo(new File("README.md"))
}

task release (dependsOn: ['setCurseForgeProjectChangelog', 'updateChangelog', 'updateReadme']) << {
    println "Released v" + project.version
}

def readCurrentChangelog() {
    def changelog = ''
    def ended = false
    new File("CHANGELOG").eachLine { line ->
        if (!ended) {
            if (line.equals('')) {
                ended = true
            } else {
                changelog += line + "\n"
            }
        }
    }
    return changelog
}
